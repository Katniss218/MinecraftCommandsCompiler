namespace katniss.trap.spike_trap

function _spawn
{
    execute align xyz positioned ~0.5 ~ ~0.5 run summon armor_stand ~ ~ ~ {Tags:["katniss.traps.spike_trap"],NoGravity:1b,Silent:1b,Invulnerable:1b,ShowArms:1b,Marker:1b,Invisible:1b,ArmorItems:[{},{},{},{id:"minecraft:diamond_hoe",Count:1b,tag:{CustomModelData:3}}]}
    execute align xyz positioned ~0.5 ~ ~0.5 as @e[distance=..0.0001] run scoreboard players set @s npc_ai1 0
    execute align xyz positioned ~0.5 ~ ~0.5 as @e[distance=..0.0001] run tp @s ~ ~-1 ~
}

function _remove
{
    execute align xyz positioned ~0.5 ~-0.5 ~0.5 run kill @e[type=armor_stand,tag=katniss.traps.spike_trap,distance=..1]
}

function extend
{
    tp @s ~ ~0.1 ~
    scoreboard players add @s npc_ai1 1
}

function retract
{
    tp @s ~ ~-0.1 ~
    scoreboard players remove @s npc_ai1 1

    execute unless score @s npc_ai2 matches 1 run playsound minecraft:block.iron_trapdoor.open hostile @a ~ ~ ~ 1 0.4
    execute unless score @s npc_ai2 matches 1 run scoreboard players set @s npc_ai2 1
    scoreboard players set @s npc_ai3 0
}

[tick]
function ___tick
{
    execute as @e[type=armor_stand,tag=katniss.traps.spike_trap] at @s run 
    {
        execute if score @s npc_ai1 matches ..9 positioned ~ ~1 ~ if entity @a[distance=..1.5] positioned ~ ~-1 ~ run function extend
        execute if score @s npc_ai1 matches 3..10 positioned ~ ~1 ~ if entity @a[distance=..1.5] positioned ~ ~-1 ~ run
        {
            execute if score @s npc_ai3 matches 0 positioned ~ ~0.5 ~ run effect give @a[distance=..1] instant_damage
            execute if entity @a[distance=..1] run scoreboard players add @s npc_ai3 1
            execute if score @s npc_ai3 matches 20.. run scoreboard players set @s npc_ai3 0
        }
        
        execute if score @s npc_ai1 matches 1..10 positioned ~ ~1 ~ unless entity @a[distance=..1.5] positioned ~ ~-1 ~ run function retract
        execute if score @s npc_ai1 matches 0 if score @s npc_ai2 matches 1 run scoreboard players set @s npc_ai2 0

        execute if score @s npc_ai1 matches 1 positioned ~ ~1 ~ if entity @a[distance=..1.5] run playsound minecraft:block.iron_trapdoor.open hostile @a ~ ~ ~ 1 0.1
    }
}